plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}


def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    namespace = "com.plany.app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "29.0.13599879"


    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }


    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


    kotlinOptions {
        jvmTarget = '17'
    }
   
    lintOptions {
        checkReleaseBuilds false
        disable 'InvalidPackage'
        disable 'ObsoleteSdkInt'
        disable 'Deprecation'
    }


    defaultConfig {
        applicationId = "com.plany.app"
        minSdk = 23
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }


    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


flutter {
    source = "../.."
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
}


tasks.withType(JavaCompile) {
    options.deprecation = true
    options.compilerArgs += ["-Xlint:deprecation"]
}
